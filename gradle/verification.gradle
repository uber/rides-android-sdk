subprojects {
    buildscript {
        repositories {
            google()
            jcenter()
        }
    }

    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }

    apply plugin: 'checkstyle'

    afterEvaluate {
        if (project.getPlugins().hasPlugin('com.android.application') ||
                project.getPlugins().hasPlugin('com.android.library')) {

            task checkstyleMain(type: Checkstyle) {
                ignoreFailures = true
                showViolations = true
                source 'src/main', 'src/release'
                include '**/*.java'
                exclude '**/gen/**'
                exclude '**/R.java'
                exclude '**/BuildConfig.java'
                reports {
                    xml.destination "$project.buildDir/reports/checkstyle/main.xml"
                }
                classpath = files()
                configFile = rootProject.file('checkstyle.xml')
            }

            task checkstyleTest(type: Checkstyle){
                ignoreFailures = true
                showViolations = true
                source 'src/test', 'src/androidTest'
                include '**/*.java'
                exclude '**/gen/**'
                exclude '**/R.java'
                exclude '**/BuildConfig.java'
                reports {
                    xml.destination "$project.buildDir/reports/checkstyle/test.xml"
                }
                classpath = files()
                configFile = rootProject.file('checkstyle.xml')
            }

            task checkstyle(dependsOn:['checkstyleMain', 'checkstyleTest']){
                description 'Runs Checkstyle inspection against Android sourcesets.'
                group = 'Code Quality'
            }

            project.tasks.getByName("check").dependsOn "checkstyle"
        } else {
            checkstyle {
                ignoreFailures = true
                showViolations = true
                configFile rootProject.file('checkstyle.xml')
            }
        }
    }
}